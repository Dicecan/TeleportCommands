plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow'
}

base {
    archivesName = "${mod_id}-${project.name}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository

    maven {
        name = 'Sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
    implementation 'org.tomlj:tomlj:1.1.1'
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
//    publishing.publications.configureEach {
//        suppressPomMetadataWarningsFor(variant)
//    }
}

shadowJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }

    dependencies {
        include(dependency('org.tomlj:tomlj:1.1.1'))
        relocate 'org.tomlj', "dev.mrsnowy.tomlj_${mod_id}"
    }

    exclude('mappings/**')
}

processResources {
    def expandProps = [
            "version": version,
            "group": project.group, //Else we target the task's group.
            "minecraft_version": minecraft_version,
            "minecraft_version_range": minecraft_version_range,
            "parchment_mappings": parchment_mappings,
//            "fabric_api": fabric_api,
            "fabric_loader_version": fabric_loader_version,
            "fabric_loom": fabric_loom,
            "neoforge_version": neoforge_version,
            "neoforge_loader_version_range": neoforge_loader_version_range,
            "quilt_loader_version": quilt_loader_version,
            "quilt_fabric_api": quilt_fabric_api,
            "quilt_loom": quilt_loom,
            "mod_name": mod_name,
            "mod_author": mod_author,
            "mod_id": mod_id,
            "license": license,
            "description": project.description,
            "credits": credits,
            "java_version": java_version
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'quilt.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}
